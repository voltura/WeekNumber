@ECHO OFF
TITLE Compiling WeekNumber installer...

:: VARIABLES
SET "INSTALL_SCRIPT_FOLDER=%~dp0"
SET "INSTALL_SCRIPT_FOLDER=%INSTALL_SCRIPT_FOLDER:~0,-1%"
SET VERSION=%1
SET NSIS_INSTALL_FOLDER="C:\Program Files (x86)\NSIS"
SET INSTALLER="%INSTALL_SCRIPT_FOLDER%\WeekNumber_%VERSION%_Installer.exe"
SET INSTALL_LOG="%INSTALL_SCRIPT_FOLDER%\WeekNumber_%VERSION%_Installer.log"
SET INSTALL_SCRIPT="%INSTALL_SCRIPT_FOLDER%\WeekNumber.nsi"

:: CHECK IF COMPILER EXIST
IF NOT EXIST "%NSIS_INSTALL_FOLDER%\makensis.exe" CALL :ERROR 100

:: REMOVE EXISTING INSTALLER WITH SAME VERSION
IF EXIST %INSTALLER% DEL /F /Q %INSTALLER% >NUL 2>&1

:: COMPILE INSTALL SCRIPT
CD /D %NSIS_INSTALL_FOLDER%
ECHO %DATE% %TIME% Compiling %INSTALL_SCRIPT%... >%INSTALL_LOG%
makensis.exe /DVERSION=%VERSION% /V1 %INSTALL_SCRIPT%
SET RESULT=%ERRORLEVEL%
CD /D "%INSTALL_SCRIPT_FOLDER%"
ECHO %DATE% %TIME% Compile operation completed with result %RESULT% >>%INSTALL_LOG%

:: RETURN RESULT
IF EXIST %INSTALLER% EXIT %RESULT%
CALL :ERROR %RESULT%

:ERROR
SET ERROR_RESULT=%~1
IF %ERROR_RESULT% EQU 100 (
	@ECHO %DATE% %TIME% NSIS not found. Installation script could not be compiled. >>%INSTALL_LOG%
) ELSE (
	@ECHO %DATE% %TIME% Installation script compilation failed with error code %ERROR_RESULT%. >>%INSTALL_LOG%
)
EXIT %ERROR_RESULT%